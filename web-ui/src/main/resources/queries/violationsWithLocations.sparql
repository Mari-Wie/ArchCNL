PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX conformance: <http://arch-ont.org/ontologies/architectureconformance#>
PREFIX famix: <http://arch-ont.org/ontologies/famix.owl#>
PREFIX architecture: <http://www.arch-ont.org/ontologies/architecture.owl#>
SELECT DISTINCT ?Rule ?Violation ?StatementType ?Subject ?Predicate ?Object
WHERE {
GRAPH ?g
  {
    ?rule rdf:type conformance:ArchitectureRule.
    ?rule conformance:hasRuleRepresentation ?Rule.
    ?Violation conformance:violates ?rule.
    ?proof conformance:proofs ?Violation.
    OPTIONAL {
      {?proof conformance:hasAssertedStatement ?aStatement.}
      UNION
      {?proof conformance:hasNotInferredStatement ?nStatement.}
      FILTER(?statement in (?aStatement, ?nStatement))
      BIND(IF(BOUND(?aStatement), "Asserted", "NotInferred") AS ?StatementType)
      
      ?statement conformance:hasSubject ?S.
             OPTIONAL {
               ?S famix:isLocatedAt ?SubjectLocation.
             }
      BIND(IF(BOUND(?SubjectLocation), ?SubjectLocation, ?S) AS ?Subject)

      ?statement conformance:hasPredicate ?P.
               OPTIONAL {
               ?P famix:isLocatedAt ?PredicateLocation.
             }
      BIND(IF(BOUND(?PredicateLocation), ?PredicateLocation, ?P) AS ?Predicate)

      ?statement conformance:hasObject ?O.
               OPTIONAL {
               ?O famix:isLocatedAt ?ObjectLocation.
             }
      BIND(IF(BOUND(?ObjectLocation), ?ObjectLocation, ?O) AS ?Object)
    }
  }
}
ORDER BY ?Violation